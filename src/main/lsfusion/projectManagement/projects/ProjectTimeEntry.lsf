MODULE ProjectTimeEntry;

REQUIRE Project, TimeEntry;

NAMESPACE ProjectManagement;

project = DATA Project (TimeEntry);
nameProject 'Project' (TimeEntry e) = name(project(e));
nameCustomer 'Partner' (TimeEntry e) = namePartner(project(e));

countTimeEntry 'Time entries' (Project p) = GROUP SUM 1 IF project(TimeEntry t) = p;  
hoursTimeEntry 'Hours' (Project p) = GROUP SUM hours(TimeEntry t) IF project(t) = p;  

EXTEND FORM timeEntry
    PROPERTIES(t) nameProject
;

DESIGN timeEntry {
    header {
        MOVE PROPERTY(nameProject(t));
    }
}

EXTEND FORM timeEntries
    PROPERTIES(t) READONLY nameProject
    
    LIST TimeEntry OBJECT t
;

EXTEND FORM project
    OBJECTS t = TimeEntry
    PROPERTIES(t) nameEmployee, date, hours, description, NEW, DELETE
    FILTERS project(t) = p
;

DESIGN project {
    tabs {
        NEW timeEntries {
            caption = (CONCAT ' ', 'Time entries', '(' + countTimeEntry(p) + ')', '(' + hoursTimeEntry(p) + ')');
            MOVE BOX(t) { caption = ''; }
        }
    }
}

EXTEND FORM projects
    PROPERTIES(p) READONLY countTimeEntry, hoursTimeEntry
;