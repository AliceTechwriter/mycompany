MODULE Project;

REQUIRE ProjectManagementSettings, ProjectType, MetaNumerator, Company;

NAMESPACE ProjectManagement;

CLASS Project 'Проект';

@defineID(project, 'Проекты', 'ПР', 6);

type 'Тип' = DATA ProjectType (Project) NONULL;
nameType 'Тип' (Project s) = name(type(s));
WHEN LOCAL SET(Project s IS Project) AND NOT CHANGED(type(s)) DO type(s) <- defaultProjectType(); 

name 'Наименование' = DATA STRING (Project) CHARWIDTH 20 IN id;

company 'Компания' = DATA Company (Project);
nameCompany 'Компания' (Project p) = name(company(p));
WHEN LOCAL SET(Project p IS Project) AND NOT CHANGED(company(p)) DO company(p) <- defaultCompany();

partner 'Контрагент' = DATA Partner (Project);
namePartner 'Контрагент' (Project p) = name(partner(p)) IN id;

startDate 'Дата начала' = DATA DATE (Project) IN id;
startDate(Project p) <- currentDate() WHEN SET(p IS Project);
endDate 'Дата окончания' = DATA DATE (Project);

description 'Описание' = DATA RICHTEXT (Project);

FORM project 'Проект'
    OBJECTS p = Project PANEL
    
    PROPERTIES(p) nameType, id, name, nameCompany, namePartner, startDate, endDate,
                  description
    
    EDIT Project OBJECT p
;

DESIGN project {
    caption = CONCAT ' ', 'Проект', name(p), id(p);
    OBJECTS {        
        NEW statusPane FIRST {
            caption = 'Статусы';
            type = CONTAINERH;
            alignment = STRETCH;                          
            NEW statusActions {
                type = CONTAINERH;                    
                flex = 1; 
                NEW primaryActions {
                    type = CONTAINERH;
                }
                NEW secondaryActions {
                    type = CONTAINERH;
                }           
            }
            NEW status {
                type = CONTAINERH;
            }
        }    
        NEW header {
            alignment = STRETCH;   
            type = CONTAINERH;
            NEW headerLeft {
                MOVE PROPERTY(nameType(p));                   
                MOVE PROPERTY(id(p));
                MOVE PROPERTY(name(p));                   
                MOVE PROPERTY(nameCompany(p));
                MOVE PROPERTY(namePartner(p));
            }
            NEW headerRight {
                MOVE PROPERTY(startDate(p));
                MOVE PROPERTY(endDate(p));
            }    
            NEW relatedDoc {
                type = TABBED;
                fill = 1;
            }                                 
        }
        NEW tabs {
            type = TABBED;
            fill = 1;
            NEW description {
                caption = 'Описание';
                MOVE PROPERTY(description(p)) { caption = ''; fill = 1; }
            }
        }
    }  
}

FORM projects 'Проекты'
    OBJECTS p = Project

    PROPERTIES(p) READONLY nameType, id, name, nameCompany, namePartner, startDate, endDate
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE
    
    LIST Project OBJECT p
;

NAVIGATOR {
    operations {
        NEW projects;
    }
}