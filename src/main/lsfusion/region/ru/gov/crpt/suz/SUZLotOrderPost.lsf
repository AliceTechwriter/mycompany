MODULE SUZLotOrderPost;

REQUIRE SUZAuthentication, SUZLotOrder, CryptoPro;

NAMESPACE SUZ;

EXTEND CLASS LotOrderStatus {
    posted 'Отправлен'
}
posted 'Отправлен' = DATA BOOLEAN (LotOrder);

status (LotOrder s) += WHEN posted(s) THEN LotOrderStatus.posted;
colorStatus (LotOrder s) += WHEN status(s) = LotOrderStatus.posted THEN RGB(252,247,149);


lotOrderUrl (LotOrder o) = url() + '/api/v2/' + idType(o);

quantity (LotOrder o, STRING[14] gtin) = GROUP SUM quantity(LotOrderLine d) IF lotOrder(d) = o AND gtin = gtinProduct(d); 

FORM postLotOrder
    OBJECTS o = LotOrder PANEL
    
    PROPERTIES contactPerson = (OVERRIDE name(currentUser()), 'Администратор'), releaseMethodType = 'REMAINS', createMethodType = 'SELF_MADE'
    
    OBJECTS products = STRING[14]
    PROPERTIES gtin = VALUE(products), 
               quantity = quantity(o, products), 
               serialNumberType = 'OPERATOR' IF products IS STRING[14], 
               templateId = 10 IF products IS STRING[14], 
               cisType = 'UNIT' IF products IS STRING[14]
    FILTERS quantity(o, products)
;

post 'Отправить в СУЗ' (LotOrder o) {
    APPLY; 
    IF canceled() THEN RETURN;

    NEWSESSION {
        EXPORT postLotOrder OBJECTS o = o JSON;
        
        sign(exportFile(), TRUE);
        
        suzToken() <- token(company(o));
        LOCAL result = FILE();
        TRY {
            EXTERNAL HTTP POST lotOrderUrl(o) + '/orders?omsId=' + omsId(company(o)) HEADERS suzSignedHeaders PARAMS exportFile() TO result;
        } CATCH {
            fileToString(result());
            MESSAGE 'Ошибка при оправке документа в СУЗ : \n' + (OVERRIDE messageCaughtException(), '') + '\n' + (OVERRIDE resultString(), '');
            RETURN;
        }
        
        LOCAL orderId = STRING[100] ();
        IMPORT JSON FROM result() TO() orderId;
        id(o) <- orderId();
        posted(o) <- TRUE;
        APPLY;

        MESSAGE 'Документ успешно отослан. Присвоен код : ' + id(o);
    } 
}

EXTEND FORM lotOrder
    PROPERTIES(o) post SHOWIF status(o) = LotOrderStatus.draft, posted 
;

DESIGN lotOrder {
    primaryActions {
        MOVE PROPERTY(post(o)) { fontStyle = 'bold'; }
    }                
    status {
        MOVE PROPERTY(posted(o));                               
    }
}

EXTEND FORM lotOrders    
    EXTEND FILTERGROUP status
        FILTER 'Отправлен' status(o) = LotOrderStatus.posted    
;

// constraint
CONSTRAINT (CHANGED(product(LotOrderLine l)) OR CHANGED(quantity(l))) AND (PREV(posted(lotOrder(l))) OR posted(lotOrder(l)))
    MESSAGE 'Запрещено редактировать заказы марок после их отсылки в СУЗ';
